
.model tiny
locals

; Темп (в ударах в минуту)
tempo   equ 120

; Таблица соответствия номеров полутонов обозначениям нот 
; (в буквенной нотации), S - диез
C   equ 0   ;"до"
D   equ 2   ;"ре"
E   equ 4   ;"ми"
F   equ 5   ;"фа"
G   equ 7   ;"соль"
A   equ 9   ;"ля"
B   equ 11  ;"си"
S   equ 1   ;#

;---------------------------------------------------------------
; play - воспроизведение ноты
;   note = номер полутона
;   octave = номер октавы
;   len = длительность для размера 4/4 и заданного темпа

play macro note,octave,len
    mov ax,octave shl 8 + note  ;начинаем играть ноту
    call play_note
    mov cx,4370/(tempo*len)     ;задержка
    call delay
    call stop_note              ;выключаем динамик
endm

.code
.startup
    ;воспроизведение ноты "ля" первой октавы
    ;play A,3,16
    ;play D,3,8
    play E,4,8
    mov cx, 4370/(tempo*30)
    call delay
    play E,4,8


    ;выход
    mov ax,4C00h
    int 21h

;---------------------------------------------------------------
; play_note - начинает воспроизведение ноты
; вход:
;   al = номер полутона внутри октавы (0..11)
;   ah = номер октавы (3 = первая фортепианная)
; выход:
;   нет

play_note proc
    mov cl,ah       ;cl = номер октавы
    cbw             ;ax = номер полутона * 2
    shl ax,1
    lea bx,notes    ;bx = @notes[номер полутона]
    add bx,ax
    mov al,0B6h     ;загрузка упраляющего слова таймера
    out 43h,al      ;(меандер на 3-м канале)
    mov ax,[bx]     ;ax = notes[номер полутона] * 2^номер октавы
    shr ax,cl
    out 42h,al      ;загрузка коэффициента деления в 3-й канал таймера
    mov al,ah
    out 42h,al
    in al,61h       ;включение динамика
    or al,3
    out 61h,al
    ret
endp

;---------------------------------------------------------------
; stop_note - выключает динамик
; вход:
;   нет
; выход:
;   нет

stop_note proc
    in al,61h
    and al,not 1
    out 61h,al
    ret
endp

;---------------------------------------------------------------
; delay - задержка
; вход:
;   cx = длительность задержки в 1/18 с (точнее - в 65536/1193180 с)
; выход:
;   нет

delay proc
    xor ax,ax
    mov es,ax
@@1:
    mov ax,es:[46Ch]
@@2:
    hlt
    cmp ax,es:[46Ch]
    je @@2
    loop @@1
    ret
endp

.const

; notes - таблица коэффициентов деления для октавы 0 (контроктавы)
;   Коэффициенты деления для более высоких октав получаются 
;   путём умножения значений из таблицы на 2^(номер октавы)

notes:
    dw 36488    ;C
    dw 34445    ;C#
    dw 32511    ;D
    dw 30673    ;D#
    dw 28960    ;E
    dw 27328    ;F
    dw 25804    ;F#
    dw 24350    ;G
    dw 22981    ;G#
    dw 21694    ;A
    dw 20473    ;A#
    dw 19325    ;B

end